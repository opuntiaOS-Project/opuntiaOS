# Copyright 2021 Nikita Melekhin. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

template("oneOS_static_library") {
  assert(defined(invoker.sources),
         "Need sources in $target_name to build static library")

  lib_name = target_name
  lib_build_name = lib_name + "_build"
  lib_include_config_name = lib_name + "_include_config"
  compiled_lib_output_name_base = "$root_out_dir/tmp/libs/"
  compiled_lib_output_name = compiled_lib_output_name_base + lib_name
  final_lib_output_name = "$root_out_dir/base/libs/" + lib_name + ".a"
  deplib_list = []
  deplib_bulders_list = []
  includes = []

  if (defined(invoker.include_dirs)) {
    includes = invoker.include_dirs
  }

  if (defined(invoker.deplibs)) {
    foreach(i, invoker.deplibs) {
      fullname = "//libs/" + i + ":" + i
      deplib_bulders_list += [ fullname + "_build" ]
      deplib_list += [ compiled_lib_output_name_base +
                       get_label_info(fullname, "name") + ".a" ]
      includes += [ "//libs/" + i + "/include" ]

      # Also adding libc includes.
      # Note to add libc after libcxx.
      if (i == "libcxx") {
        includes += [ "//libs/libc/include" ]
      }
    }
  }
  includes += [ "//libs/" + lib_name + "/include" ]

  deplib_list += [ compiled_lib_output_name + ".a" ]
  deplib_bulders_list += [ "//libs/" + lib_name + ":" + lib_build_name ]

  # Use a strange __EMPTY_PATH_, empty string can't be passed as an arg.
  path_to_bins = "__EMPTY_PATH_"
  if (host == "llvm") {
    path_to_bins = llvm_bin_path
  }

  script_args = [
    "$target_cpu",
    "$host",
    "$path_to_bins",
    rebase_path("$final_lib_output_name", root_build_dir),
  ]

  foreach(i, deplib_list) {
    script_args += [ rebase_path(i, root_build_dir) ]
  }

  config(lib_include_config_name) {
    include_dirs = includes
  }

  # Create a build rule to compile only a lib with unresolved references from other libs
  static_library(lib_build_name) {
    output_name = "tmp/libs/" + lib_name
    sources = invoker.sources
    include_dirs = includes
    forward_variables_from(invoker,
                           [
                             "configs",
                             "cflags",
                             "cflags_c",
                             "cflags_cc",
                             "asmflags",
                             "deps",
                             "public_deps",
                           ])
  }

  action(lib_name) {
    script = "//build/libs/merge_libs.py"
    inputs = deplib_list
    outputs = [ "$final_lib_output_name" ]
    deps = deplib_bulders_list
    args = script_args
  }
}
